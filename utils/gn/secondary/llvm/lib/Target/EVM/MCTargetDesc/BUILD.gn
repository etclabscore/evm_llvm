import("//llvm/utils/TableGen/tablegen.gni")

tablegen("EVMGenAsmWriter") {
  visibility = [ ":MCTargetDesc" ]
  args = [ "-gen-asm-writer" ]
  td_file = "../EVM.td"
}

tablegen("EVMGenInstrInfo") {
  visibility = [ ":MCTargetDesc" ]
  args = [ "-gen-instr-info" ]
  td_file = "../EVM.td"
}

tablegen("EVMGenMCCodeEmitter") {
  visibility = [ ":MCTargetDesc" ]
  args = [ "-gen-emitter" ]
  td_file = "../EVM.td"
}

tablegen("EVMGenRegisterInfo") {
  visibility = [ ":MCTargetDesc" ]
  args = [ "-gen-register-info" ]
  td_file = "../EVM.td"
}

tablegen("EVMGenSubtargetInfo") {
  visibility = [ ":MCTargetDesc" ]
  args = [ "-gen-subtarget" ]
  td_file = "../EVM.td"
}

static_library("MCTargetDesc") {
  output_name = "LLVMEVMDesc"

  # This should contain tablegen targets generating .inc files included
  # by other targets. .inc files only used by .cpp files in this directory
  # should be in deps instead.
  public_deps = [
    ":EVMGenInstrInfo",
    ":EVMGenRegisterInfo",
    ":EVMGenSubtargetInfo",
  ]
  deps = [
    ":EVMGenAsmWriter",
    ":EVMGenMCCodeEmitter",
    "//llvm/lib/MC",
    "//llvm/lib/MC/MCDisassembler",
    "//llvm/lib/Support",
    "//llvm/lib/Target/EVM/TargetInfo",
  ]
  include_dirs = [ ".." ]
  sources = [
    "EVMAsmBackend.cpp",
    "EVMELFObjectWriter.cpp",
    "EVMInstPrinter.cpp",
    "EVMMCCodeEmitter.cpp",
    "EVMMCTargetDesc.cpp",
  ]
}
